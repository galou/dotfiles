#!/usr/bin/perl
#
# Copyright 2014 Pierre Mavro <deimos@deimos.fr>
# Copyright 2014 Vivien Didelot <vivien@didelot.org>
#
# Licensed under the terms of the GNU GPL v3, or any later version.
#
# This script is meant to use with i3blocks. It parses the output of the "acpi"
# command (often provided by a package of the same name) to read the status of
# the battery, and eventually its remaining time (to full charge or discharge).
#
# The color will gradually change for a percentage below 85%, and the urgency
# (exit code 33) is set if there is less that 5% remaining.

use strict;
use warnings;
use utf8;

my $acpi;
my $status;
my $percent;
my $full_text;
my $short_text;
my $bat_number = $ENV{BLOCK_INSTANCE} || 0;

# read the first line of the "acpi" command output
open (ACPI, "acpi -b | grep 'Battery $bat_number' |") or die;
$acpi = <ACPI>;
close(ACPI);

# fail on unexpected output
if ($acpi !~ /: (\w+ ?\w+), (\d+)%/) {
	die "$acpi\n";
}

$status = $1;
$percent = $2;

if ($percent < 20) {
	$full_text = "";  # U+f244.
} elsif ($percent < 33) {
	$full_text = "";  # U+f243.
} elsif ($percent < 66) {
	$full_text = "";  # U+f242.
} elsif ($percent < 85) {
	$full_text = "";  # U+f241.
} else {
	$full_text = "";  # U+f240.
}

$full_text .= " $percent%";

if ($status eq 'Charging') {
	$full_text .= '  ';  # U+f1e6, `plug` with FontAwesome 6.
}

if ($status eq 'Not charging') {
	# $full_text .= '  ';  # U+e55b, `plug-circle-colt` with FontAwesome 6.
	$full_text .= '  ';  # U+e560.
}

# if ($status eq 'Charging' || $status eq 'Not charging') {
# 	$full_text .= ' ';  # U+f1e6, `plug` with FontAwesome 6.
# }

$short_text = $full_text;

if ($acpi =~ /(\d\d:\d\d):/) {
	$full_text .= " ($1)";
}

# print text
print "$full_text\n";
print "$short_text\n";

# consider color and urgent flag only on discharge
if ($status eq 'Discharging') {

	if ($percent < 20) {
		print "#FF0000\n";
	} elsif ($percent < 40) {
		print "#FFAE00\n";
	} elsif ($percent < 60) {
		print "#FFF600\n";
	} elsif ($percent < 85) {
		print "#A8FF00\n";
	}

	if ($percent < 5) {
		exit(33);
	}
}

if ($ENV{BLOCK_BUTTON} == 1) {
	system("gnome-power-statistics &");
}

exit(0);
